// Load in a dataset of compounds
data = new data.materials.CompositionDataset
data import ../datasets/landolt.txt

// Get the most optimistic measure of GFA
data duplicates RankingDuplicateResolver minmize PropertyRanker gfa SimpleEntryRanker

// Replace all AC entries with CR
data target gfa
data modify ClassEliminationModifier AC CR

// Generate new attributes
data attributes properties directory ../magpie/lookup-data/
data attributes properties add set general
data attributes generate

// Create a model using Weka, then test it
model = new models.classification.WekaClassifier trees.RandomForest -I 400 -K 16 -num-slots 0
model train $data
cv_data = model crossvalidate $data 10
save cv_data cv_data stats

// Print out training statistics
print model validation stats
print model validation roc

// Save model
save model gfa-model

// Save data template
save data gfa-data template
save data gfa-training-data

// Perform attribute selection
rf_sel = new attributes.selectors.RandomForestAttributeSelector -num_attr 20 -num_trees 200
dedup_sel = new attributes.selectors.RemoveCorrelatedAttributeSelector -useclass -kendall 0.9
sel = new attributes.selectors.StagedAttributeSelector $dedup_sel $rf_sel
sel train $data

exit
